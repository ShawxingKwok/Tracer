plugins {
    id 'org.jetbrains.kotlin.jvm'
    id 'maven-publish'
    id 'signing'
}

test {
    useJUnitPlatform()
}

kotlin{
    jvmToolchain(8)
}

dependencies {
    implementation lib_kt_util
    implementation lib_kotlin_reflect

    implementation lib_ksp
    implementation lib_ksp_util

    implementation project(":annotations")
    implementation lib_tracer_common_annotations
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach{
    kotlinOptions {
        freeCompilerArgs = [
                "-Xexplicit-api=warning",
                "-Xcontext-receivers",
                "-opt-in=com.google.devtools.ksp.KspExperimental",
                "-opt-in=kotlin.contracts.ExperimentalContracts",
        ]
    }
}

//region publish
group = my_group
version = version_common_processor

java {
    withJavadocJar()
    withSourcesJar()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'tracer-common-processor'
            from components.java
            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
            pom {
                name = 'Tracer common processor'
                description = 'This is for extension with the public `MyProvider` and no `resources`.'
                url = "https://$my_url"
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'ApolloKwok'
                        name = 'Apollo Kwok'
                        email = 'ApolloKwok@126.com'
                    }
                }
                scm {
                    connection = "scm:git:git://@${my_url}.git"
                    developerConnection = "scm:git:ssh://@${my_url}.git"
                    url = "https://$my_url"
                }
            }
        }
    }
    repositories {
        maven {
            if (publishMode == "release")
                url 'https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/'
            else
                url "https://s01.oss.sonatype.org/content/repositories/snapshots/"

            credentials {
                username = USERNAME
                password = PASSWORD
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}

javadoc {
    if(JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}
//endregion